#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vRoomInited, 1)

    load.view(viewRoger)
    load.view(viewRogerSwim)
    load.view(viewMist)
   
    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 100)
            assignn(vEgoY, 110)
        }
    }

    position(objMist, 1, 166)
    position(objMist2, 43, 166)
    position(objMist3, 85, 166)
    position(objMist4, 127, 166)

    // add wider range to reduce instances of encounter
    random(1, 100, vDiceRoll) 

    reset(fGotcha)
    if(lessn(vDiceRoll, vEnemyChance)) {
        load.view(viewWizard)
        set.view(objMonster, viewWizard)
        draw(objMonster)
        position(objMonster, 15, 50)        
        follow.ego(objMonster, 2, fGotcha)  

        load.sound(sndDevMonster)
        sound(sndDevMonster, fSoundDone)    
    }

    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(50)
}



if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 155)
        assignn(vEgoDirection, directionUp)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavRockE4)   
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWalnutF5)     
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWitchHouseD5)     
    }        
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(isset(fGotcha)) {
    reset(fGotcha)
    if(!isset(fDead)) { 
        
        //stop.cycling(objMonster)
        //stop.motion(objEgo)
        //stop.motion(objMonster)
        load.view(viewRogerStoned)
        set.view(objEgo, viewRogerStoned)
        assignn(vDeathReason, valXsStoned)
        end.of.loop(objEgo, fRogerStoned)    
    }
}

if(isset(fRogerStoned)) {
    assignn(vEgoDirection, directionStopped)
    assignn(vDeathReason, valXsStoned)
    set(fDead)
    wander(objMonster)
}

if(said("look")) {
    print("You are at the edge of a thick forest. A clearing sits aside of the wooded area. There's a thick fog to the south.")
}
if(said("look", "room")) {
    print("You are at the edge of a thick forest. A clearing sits aside of the wooded area. There's a thick fog to the south.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.")
}

return()
#message 1 "Wizard"
