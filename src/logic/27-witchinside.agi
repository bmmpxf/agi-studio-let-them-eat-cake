#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewWitchCabinet)
    load.view(viewPaper)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objWitchCabinet, viewWitchCabinet)
    set.view(objPaper, viewPaper)

    stop.cycling(objWitchCabinet)

    draw(objEgo)
    draw(objWitchCabinet)
    draw(objPaper)
    
    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 50)
            assignn(vEgoY, 140)
        }
    }

    position.v(objEgo, vEgoX, vEgoY)

    position(objWitchCabinet, 35, 80)

    if(!has(invPaper)) {
        position(objPaper, 55, 120)
        set.priority(objPaper, 13)
    }

    assignn(vEgoEdgeTouched, edgeNone)

    reset(fCabinetOpen)
    set.priority(objEgo, 14)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 51)
        assignn(vEgoY, 133)
        set.priority(objEgo, 11)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWitchHouseD5)   
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(isset(fCabinetOpen)) {
    if(!has(invCake)) {
        load.view(viewCake) 
        set.view(objCake, viewCake)
        draw(objCake)
        set.cel(objCake, 0)
        position(objCake, 35, 69)
        set.priority(objCake, 10)
    }
}

if(said("look")) {
    print("You stand in a large open room with access to a small bedroom in the back. The inside of the house is clean and tidy. There's a large oven here and a cage... That's right... A CAGE!? Not cool. Better get out of here.")
}
if(said("look", "room")) {
    print("You stand in a large open room with access to a small bedroom in the back. The inside of the house is clean and tidy. There's a large oven here and a cage... That's right... A CAGE!? Not cool. Better get out of here.")
}
if(said("look", "table")) {
    if(!has(invPaper)) {
        print("There's a large table here with a piece of paper on it.")
    }
    if(has(invPaper)) {
        print("There's a large table here.")
    }
}
if(said("look", "wall")) {
    print("There's a cupboard built into the wall.")
}
if(said("look", "cupboard")) {
    if(isset(fCabinetOpen)) {
        if(has(invCake)) {
            print("There's a large cupboard here that is built into the wall.")
        }
        if(!has(invCake)) {
            print("There's a large cupboard here that is built into the wall. There's a cake on the shelf.")
        }
    }
    if(!isset(fCabinetOpen)) {
        print("There's a large cupboard here that is built into the wall.")
    }
}

if(said("close", "cupboard")) {
    if(posn(objEgo, 34, 89, 51, 92)) {
        set.cel(objWitchCabinet, 0)
    }
    if(!posn(objEgo, 34, 89, 51, 92)) {
        print("From here? Not happening.")
    }
}

if(said("open", "cupboard")) {
    if(posn(objEgo, 34, 89, 51, 92)) {
        end.of.loop(objWitchCabinet, fCabinetOpen)
    }
    if(!posn(objEgo, 34, 89, 51, 92)) {
        print("From here? Not happening.")
    }
}

if(said("take", "cake")) {
    if(has(invCake)) {
        print("You already have it.")
    }
    if(!has(invCake)) {
        if(isset(fCabinetOpen)) {
            if(posn(objEgo, 34, 89, 51, 92)) {
                get(invCake)
                erase(objCake)
                addn(vCurPoints, 1)
                print("You remove the cake from the cupboard and carry it with you.")        
            }
            if(!posn(objEgo, 34, 89, 51, 92)) {
                print("Try as you may you cannot reach it.")
            }
        }
        if(!isset(fCabinetOpen)) {
            print("Cake? What cake?")
        }        
    }    
}

if(said("take", "paper")) {
    if(has(invPaper)) {
        print("You already have it.")
    }
    if(!has(invPaper)) {
        if(posn(objEgo, 45, 129, 63, 139)) {    
            get(invPaper)
            erase(objPaper)        
            addn(vCurPoints, 1)
            set.priority(objEgo, 12)
            print("Ok")
        }
        if(!posn(objEgo, 45, 129, 63, 139)) {
            print("You can't reach it.")
        }    
    }
}

return()

#message 1 "Witch inside"