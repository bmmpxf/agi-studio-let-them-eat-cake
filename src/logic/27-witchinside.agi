#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewWitchCabinet)
    load.view(viewPaper)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objWitchCabinet, viewWitchCabinet)
    set.view(objPaper, viewPaper)

    stop.cycling(objWitchCabinet)

    draw(objEgo)
    draw(objWitchCabinet)
    draw(objPaper)
    
    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 50)
            assignn(vEgoY, 140)
        }
    }

    position.v(objEgo, vEgoX, vEgoY)

    position(objWitchCabinet, 35, 80)

    if(!has(invPaper)) {
        position(objPaper, 55, 120)
        set.priority(objPaper, 13)
    }

    assignn(vEgoEdgeTouched, edgeNone)

    reset(fCabinetOpen)
}


if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 51)
        assignn(vEgoY, 133)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWitchHouseD5)   
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("close", "cabinet")) {
    set.cel(objWitchCabinet, 0)
}

if(said("open", "cabinet")) {
    end.of.loop(objWitchCabinet, fCabinetOpen)
}

if(said("take", "cake")) {
    get(invCake)
    erase(objCake)
    addn(vCurPoints, 1)
    print("You remove the cake from the cabinet and carry it with you.")
}

if(said("take", "paper")) {
    if(!has(invPaper)) {
        get(invPaper)
        erase(objPaper)        
        addn(vCurPoints, 1)
        print("Ok")
    }
}

if(isset(fCabinetOpen)) {
    if(!has(invCake)) {
        load.view(viewCake) 
        set.view(objCake, viewCake)
        draw(objCake)
        set.cel(objCake, 0)
        position(objCake, 35, 69)
        set.priority(objCake, 10)
    }
}

if(said("look", "room")) {
    print("You stand in a large open room with access to a small bedroom in the back. The inside of the house is clean and tidy. There's a large oven here and A cage.. That's right.. A CAGE. Not cool. Better get out of here.")
}
if(said("look", "table")) {
    if(!has(invPaper)) {
        print("There's a large table here with a piece of paper on it.")
    }
    if(has(invPaper)) {
        print("There's a large table here.")
    }
}
if(said("look", "cabinet")) {
    print("There's a large cabinet here that is built into the wall.")
}

return()

#message 1 "Witch inside"