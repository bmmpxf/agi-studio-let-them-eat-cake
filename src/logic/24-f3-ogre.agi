#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)

    draw(objEgo)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 100)
            assignn(vEgoY, 100)
        }
    }

    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(55)

    // add wider range to reduce instances of encounter
    random(1, 100, vDiceRoll) 

    if(lessn(vDiceRoll, vEnemyChance)) {
        if(!isset(fGotcha)) {
            load.view(viewOgre)
            set.view(objMonster, viewOgre)
            draw(objMonster)
            position(objMonster, 80, 80)        
            follow.ego(objMonster, 2, fGotcha)    
        }
    }
}

if(isset(fGotcha)) {
    reset(fGotcha)
    if(!isset(fDead)) { 
        erase(objEgo)
        load.view(viewOgreMunch)
        set.view(objMonster, viewOgreMunch)
        normal.motion(objMonster)
        assignn(vDeathReason, valXsOgreMunch)
        stop.cycling(objMonster)
        set.cel(objMonster, 0)
        stop.motion(objMonster)
        end.of.loop(objMonster, fDead)    
    }
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavDaggerF2)     
    }

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 15)
        assignn(vEgoDirection, directionRight)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 1)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavCrashSiteA3)     
    }

    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavDwarfF4)     
    }

}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

return()

#message 1 "Ogre"
