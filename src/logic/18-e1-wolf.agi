#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewMist)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 100)
            assignn(vEgoY, 100)
        }
    }

    position(objMist, 1, 47)
    position(objMist2, 43, 47)
    position(objMist3, 85, 47)
    position(objMist4, 127, 47)

    // add wider range to reduce instances of encounter
    random(1, 100, vDiceRoll) 

    if(lessn(vDiceRoll, vEnemyChance)) {
        load.view(viewWolf)
        set.view(objMonster, viewWolf)
        draw(objMonster)
        position(objMonster, 80, 80)        
        follow.ego(objMonster, 2, fGotcha)
 

        load.sound(sndDevMonster)
        sound(sndDevMonster, fSoundDone)    
    }
    
    reset(fGotcha)
    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(50)
}

if(isset(fGotcha)) {
    reset(fGotcha)
    if(!isset(fDead)) { 
        erase(objEgo)
        load.view(viewFightCloud)
        set.view(objMonster, viewFightCloud)
        normal.motion(objMonster)
        assignn(vDeathReason, valXsWolfMunch)
        stop.cycling(objMonster)
        set.cel(objMonster, 0)
        stop.motion(objMonster)
        end.of.loop(objMonster, fDead)            
    }
}


if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoDirection, directionDown)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWellTopD1)     
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoY, 100)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavTreeF1)     
    }

    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 50)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavLakeE2)     
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("look")) {
    print("The area is covered by bushes and thick brush. There is a thick fog to the north.")
}
if(said("look", "room")) {
    print("The area is covered by bushes and thick brush. There is a thick fog to the north.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.")
}

return()
#message 1 "Wolf"
