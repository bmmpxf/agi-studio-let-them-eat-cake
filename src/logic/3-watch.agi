#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {

    // debugging scenario: Get your orders with this flag high
    // set(fWatchHasBeeped)

    unanimate.all()
    assignn(vRoomInited, 1)
    load.view(viewWatchBoss)
    load.view(viewWatchLights)

    animate.obj(objWatchBossSpeech)

    set.view(objWatchBoss, viewWatchBoss)
    set.view(objWatchBossSpeech, viewWatchBoss)
    
    set.view(objWatchPower, viewWatchLights)
    set.view(objWatchAButton, viewWatchLights)
    set.view(objWatchCButton, viewWatchLights)
    set.view(objWatchTButton, viewWatchLights)

    set.loop(objWatchBossSpeech, 1)
    start.cycling(objWatchBossSpeech)

    draw(objWatchBossSpeech)
    draw(objWatchBoss)
    draw(objWatchPower)
    draw(objWatchAButton)
    draw(objWatchCButton)
    draw(objWatchTButton)

    set.loop(objWatchAButton, 1)
    set.loop(objWatchCButton, 1)
    set.loop(objWatchTButton, 1)
    set.cel(objWatchAButton, 1)
    set.cel(objWatchCButton, 1)
    set.cel(objWatchTButton, 1)
    set.cel(objWatchPower, 1)

    stop.cycling(objWatchPower)
    stop.cycling(objWatchAButton)
    stop.cycling(objWatchCButton)
    stop.cycling(objWatchTButton)

    position(objWatchPower, 108, 61)
    position(objWatchAButton, 55, 114)
    position(objWatchCButton, 75, 114)
    position(objWatchTButton, 95, 114)

    set.priority(objWatchPower, 15)
    set.priority(objWatchAButton, 15)
    set.priority(objWatchCButton, 15)
    set.priority(objWatchTButton, 15)

    if(isset(fWatchHasBeeped)) {
        if(equaln(vHasOrders, valFalse)) {
            start.cycling(objWatchPower)
        }
    }

    assignn(vTimer, 0)
    reset(fDoJoke)
    reset(fBossGiveOrders)
    reset(fDoAction)
}

// We use the time to count cycles for some of the apps
addn(vTimer, 1)


// if(said("stop", "watch")) {
//     assignn(vRoomInited, valFalse)
//     assignn(vCurrentRoom, picHanger)
// }


if(said("look", "c")) {
    print("This is the very handy StarCon Communications app. You can use it to talk with the crew or theoretically your friends if you had any.")
}

if(said("press", "c")) {
    if(isset(fWatchHasBeeped)) {
        if(equaln(vHasOrders, valFalse)) {
            stop.cycling(objWatchPower)
            set.cel(objWatchPower, 1)

            position(objWatchBoss, 57, 83)
            set.cel(objWatchAButton, 1)
            set.cel(objWatchTButton, 1)
         
            set.cel(objWatchCButton, 0)            

            assignn(vHasOrders, valTrue)
            position(objWatchBoss, 57, 83)
            position(objWatchBossSpeech, 72, 78)
            set.priority(objWatchBossSpeech, 15)

            set(fBossGiveOrders)
            assignn(vTimer, 0)
        }
        if(equaln(vHasOrders, valFalse)) {
            print("You have no new incomming messages.")
        }
    }
    if(!isset(fWatchHasBeeped)) {
        print("You have no incomming messages at this time.")
    }
}

if(isset(fBossGiveOrders)) {
    if(equaln(vTimer, 30)) {
        print("Roger Wilco! Have you been napping in the closet again!? You were due at your next assignment an hour ago. Finish up forthwith and get over to Oribital Station 3 on the double. A Trolsdorian Muccaworm uh... 'explosively decompressed' in the flight deck bathroom. The walls are covered with an unfathomable amount Monolith Burger. Filet-o-Rat. polycheese and Tang. Clean it up! StarCon Control already has your flight plan. Move it! Commander Out.")
    }

    if(equaln(vTimer, 255)) {
        position(objWatchBoss, 0, 0)
        set.priority(objWatchBossSpeech, 1)
        set.cel(objWatchCButton, 1)
    }

}

if(said("look", "t")) {
    print("This is your time and tempature app.")
}

if(said("press", "t")) {
    position(objWatchBoss, 57, 83)
    set.cel(objWatchAButton, 1)
    set.cel(objWatchCButton, 1)
 
    set.cel(objWatchTButton, 0)
    set.loop(objWatchBoss, 3)
}


if(said("look", "a")) {
    print("This app is the Al Lowe's CyberJoke 9000 joke machine. You Love this guy even though you Har Har Hardly know him!")
}

if(said("press", "a")) {
    position(objWatchBoss, 57, 83)
    set.cel(objWatchCButton, 1)
    set.cel(objWatchTButton, 1)
 
    set.cel(objWatchAButton, 0)
    set.loop(objWatchBoss, 4)
    assignn(vTimer, 0)
    set(fDoJoke)
}

if(isset(fDoJoke)) {
    if(equaln(vTimer, 30)) {
        random(1, 5, vAlJoke)
        reset(fDoJoke)
        set.cel(objWatchAButton, 1)

        if(equaln(vAlJoke, 1)) {
            print("Working at the unemployment office must be tense knowing that if you get fired you still have to come in the next day! Find more jokes on galactiWeb at AlLowe.com")
        }
        if(equaln(vAlJoke, 2)) {
            print("I phoned the Tinnitus Help Line but there was no answer. It just kept ringing and ringing! Find more jokes on galactiWeb at AlLowe.com")
        }
        if(equaln(vAlJoke, 3)) {
            print("I shot a man with my paintball gun just to watch him dye. Find more jokes on galactiWeb at AlLowe.com")
        }
        if(equaln(vAlJoke, 4)) {
            print("If buckazoids don't grow on trees then why do banks have branches? Find more jokes on galactiWeb at AlLowe.com")
        }
        if(equaln(vAlJoke, 5)) {
            print("I'm pining for a good tree pun. I wish they were more poplar. Find more jokes on galactiWeb at AlLowe.com")
        }        
    } 
}

if(said("done", "exit")) {
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picHanger)
}

return()
#message 1 "Watch"