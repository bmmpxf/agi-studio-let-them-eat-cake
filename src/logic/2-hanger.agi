#include "0-defines.aqi"

load.sound(1)

if(equaln(vRoomInited, 0)) {

    unanimate.all()
    assignn(vRoomInited, 1)

    load.view(viewRogerSuitA)
    load.view(viewHangerSplat)
    load.view(viewBigShuttle)

    animate.obj(objEgo)

    set.view(objHangerSplat1, viewHangerSplat)
    set.view(objHangerSplat2, viewHangerSplat)
    set.view(objBigShuttle, viewBigShuttle)
    set.view(objEgo, viewRogerSuitBroom)

    start.cycling(objEgo)

    stop.cycling(objHangerSplat1)
    stop.cycling(objHangerSplat2)
    stop.cycling(objBigShuttle)

    draw(objHangerSplat1)
    draw(objHangerSplat2)
    draw(objBigShuttle)
    draw(objEgo)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 20)
            assignn(vEgoY, 100)

            assignn(vSweeping, valTrue)
            assignn(vSplat1Cleaned, valFalse)
            assignn(vSplat2Cleaned, valFalse)
            assignn(vHasOrders, valFalse)
            assignn(vCleanedUp, valFalse)
        }
    }

    position(objHangerSplat1, 100, 150)
    position(objHangerSplat2, 40, 90)
    position(objBigShuttle, 50, 120)

    position.v(objEgo, vEgoX, vEgoY)

//    set.priority(objEgo, 18)

    assignn(vTimer, 0)

}

if(!equaln(vSweeping, valTrue)){
    if(equaln(vEgoDirection, directionStopped)) {
        stop.cycling(objEgo)
    }
}

if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("stop", "sweeping")) {
    set.view(objEgo, viewRogerSuitA)
    assignn(vSweeping, valFalse)
}

if(said("start", "sweeping")) {
    set.view(objEgo, viewRogerSuitBroom)
    assignn(vSweeping, valTrue)
    start.cycling(objEgo)
}


if(said("enter", "pod")) {
    if(posn(objEgo, 53, 93, 60, 109)) {
        print("Get real...")
    }
    if(!posn(objEgo, 53, 93, 60, 109)) {
        print("You are too far away")
    }
}

if(posn(objEgo, 85, 139, 105, 151)) {
    if(!equaln(vSplat1Cleaned, valTrue)) {
        if(equaln(vSweeping, valFalse)) {
            print("You died.")
        }    
        if(equaln(vSweeping, valTrue)) {
            assignn(vSplat1Cleaned, valTrue)
            erase(objHangerSplat1)

            if(equaln(vSplat2Cleaned, valTrue)) {
                assignn(vCleanedUp, valTrue)
            }
        }    
    }
}

if(posn(objEgo, 36, 80, 48, 95)) {
    if(!equaln(vSplat2Cleaned, valTrue)) {
        if(equaln(vSweeping, valFalse)) {
            print("You died.")
        }    
        if(equaln(vSweeping, valTrue)) {
            assignn(vSplat2Cleaned, valTrue)
            erase(objHangerSplat2)

            if(equaln(vSplat1Cleaned, valTrue)) {
                assignn(vCleanedUp, valTrue)
            }
        }    
    }
}

if(equaln(vCleanedUp, valTrue)) {
    if(equaln(vTimer, 100)) {
        assignn(vRoomInited, 0)
        assignn(0, 3)
    }

    if(!equaln(vHasOrders, valTrue)) {
        addn(vTimer, 1)
    }
}

if(obj.in.box(objEgo, 151, 140, 155, 150)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 10)
    assignn(vEgoY, 126)
    assignn(vRoomInited, valFalse)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vCurrentRoom, picInnerBay)     
}

return()
#message 1 "Hanger-1"