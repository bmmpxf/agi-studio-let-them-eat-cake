#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {

    // debugging scenario: Get your orders with this flag high
    // assignn(vHasOrders, valTrue)
    // assignn(vSplat1Cleaned, valTrue)
    // assignn(vSplat2Cleaned, valTrue)
    // assignn(vCleanedUp, valTrue)

    // we're roger now. Drop Grahams inventory
    drop(invSummons)
    drop(invHat)
    drop(invTeapot)
    drop(invTeaCup)
    drop(invTea)
    get(invBroom)


    unanimate.all()
    assignn(vRoomInited, valTrue)
    load.sound(sndWatchBeep)

    load.view(viewRogerSuitBroom)
    load.view(viewRogerSuitA)
    load.view(viewHangerSplat)
    load.view(viewBigShuttle)

    animate.obj(objEgo)

    set.view(objHangerSplat1, viewHangerSplat)
    set.view(objHangerSplat2, viewHangerSplat)
    set.view(objBigShuttle, viewBigShuttle)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 20)
            assignn(vEgoY, 100)
            assignn(vSweeping, valTrue)
            set(fWearingSuit)
        }
    }

    if(equaln(vHasOrders, valFalse)) {
        assignn(vSplat1Cleaned, valFalse)
        assignn(vSplat2Cleaned, valFalse)
        assignn(vHasOrders, valFalse)
        assignn(vCleanedUp, valFalse)

        position(objHangerSplat1, 100, 150)
        position(objHangerSplat2, 40, 90)  
        
        print("What is this? Your dreams get more and more wild. You find yourself in a castle fortress unlike anything you have ever seen. Your suit of armor seems to be made of some kind of magical cloth. The stars are so close and bright that you could almost reach out and touch them. HOW STRANGE?!? And.. you're a janitor... Seriously?! Wow! That Daventry clover tea really packs a punch.")
    }

    if(equaln(vSweeping, valTrue)) {
        set.view(objEgo, viewRogerSuitBroom)
    }
    if(equaln(vSweeping, valFalse)) {
        set.view(objEgo, viewRogerSuitA)
    }

    start.cycling(objEgo)

    stop.cycling(objHangerSplat1)
    stop.cycling(objHangerSplat2)
    stop.cycling(objBigShuttle)

    draw(objHangerSplat1)
    draw(objHangerSplat2)
    draw(objBigShuttle)
    draw(objEgo)

    position(objBigShuttle, 50, 120)
    set.priority(objBigShuttle, 9)
    set.priority(objHangerSplat1, 4)
    set.priority(objHangerSplat2, 4)

    position.v(objEgo, vEgoX, vEgoY)

    assignn(vTimer, 0)
    set.horizon(73) 
}

if(!equaln(vSweeping, valTrue)){
    if(equaln(vEgoDirection, directionStopped)) {
        stop.cycling(objEgo)
    }
}

if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("stop", "sweeping")) {
    set.view(objEgo, viewRogerSuitA)
    assignn(vSweeping, valFalse)
}

if(said("start", "sweeping")) {
    set.view(objEgo, viewRogerSuitBroom)
    assignn(vSweeping, valTrue)
    start.cycling(objEgo)
}

if(said("enter", "pod")) {
    if(posn(objEgo, 31, 104, 50, 127)) {
        print("Get real... This is a serious space craft. Access to this craft requires the appropriate orders or serious clearance -- both of which you are seriosuly lacking.")
    }
    if(!posn(objEgo, 31, 104, 50, 127)) {
        print("You are too far away.")
    }
}

if(posn(objEgo, 85, 139, 105, 151)) {
    if(!equaln(vSplat1Cleaned, valTrue)) {
        if(equaln(vSweeping, valFalse)) {
            print("As your boot touches the fuel spill you immediately feel the boot material begin to melt and seperate. Within moments your suit is breached and decompresses as it equalizes with the vacuum of space. You are dead. Thanks for playing!  As usual you've been a real hoot.")
        }    
        if(equaln(vSweeping, valTrue)) {
            assignn(vSplat1Cleaned, valTrue)
            erase(objHangerSplat1)

            if(equaln(vSplat2Cleaned, valTrue)) {
                assignn(vCleanedUp, valTrue)
            }
        }    
    }
}

if(posn(objEgo, 36, 80, 48, 95)) {
    if(!equaln(vSplat2Cleaned, valTrue)) {
        if(equaln(vSweeping, valFalse)) {
            print("As your boot touches the fuel spill you immediately feel the boot material begin to melt and seperate. Within moments your suit is breached and decompresses as it equalizes with the vacuum of space. You are dead. Thanks for playing!  As usual you've been a real hoot.")
            set(fDead)
        }    
        if(equaln(vSweeping, valTrue)) {
            assignn(vSplat2Cleaned, valTrue)
            addn(vCurPoints, 1)
            erase(objHangerSplat2)

            if(equaln(vSplat1Cleaned, valTrue)) {
                addn(vCurPoints, 1)
                assignn(vCleanedUp, valTrue)
            }
        }    
    }
}

if(said("look", "watch")) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vRoomInited, valFalse)
    assignn(0, picWatch)
}

if(obj.in.box(objEgo, 151, 140, 155, 150)) {
    if(equaln(vHasOrders, valTrue)) {

        assignn(vSweeping, valFalse)

        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 10)
        assignn(vEgoY, 126)
        assignn(vRoomInited, valFalse)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vCurrentRoom, picInnerBay)         
    }
    if(equaln(vHasOrders, valFalse)) {
        print("Look at that deck!! You better keep cleaning.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 146)
        assignn(vEgoDirection, directionLeft)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}

if(equaln(vCleanedUp, valTrue)) {
    if(equaln(vTimer, 100)) {
        if(!isset(fWatchHasBeeped)) {
            set(fWatchHasBeeped)
            sound(sndWatchBeep, fDoAction)
            print("A long beep is emitted from the watch on your arm.")    
        }
    }
 
    if(!equaln(vHasOrders, valTrue)) {
        addn(vTimer, 1)
    }
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        stop.cycling(objEgo)
        set.view(objEgo, viewRogerSuitA)
        move.to(objEgo, 165, 0, 3, fDoAction)
        set(fDead)
    }
}

if(!isset(fWearingSuit)) {
    print("Surgeon General's Warning: Exposing yourself to the vacuum of space is hazardous to your health.  You are dead. Thanks for playing.")
    set(fDead)
}

return()

#message 1 "Hanger-1"