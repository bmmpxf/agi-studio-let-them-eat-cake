#include "0-defines.aqi"

load.sound(1)

if(equaln(vRoomInited, 0)) {

    unanimate.all()
    assignn(vGameCycleDelay, 3)
    assignn(vRoomInited, 1)

    erase(viewSummons)
    discard.view(viewSummons)
    erase(viewHat)
    discard.view(viewHat)
    erase(viewTeapot)
    discard.view(viewTeapot)
    erase(viewTeaCup)
    discard.view(viewTeaCup)
    erase(viewGrahamSleep)
    discard.view(viewGrahamSleep)

    load.view(viewRogerSuitBroom)
    load.view(viewRogerSuitA)
    load.view(viewBigShuttle)
    load.view(viewHangerSplat)
    load.view(viewPod)
    load.view(viewPodPlatform)

    animate.obj(objEgo)

    set.view(objEgo, viewRogerSuitBroom)
    set.view(objBigShuttle, viewBigShuttle)
    set.view(objHangerSplat1, viewHangerSplat)
    set.view(objHangerSplat2, viewHangerSplat)
    set.view(objPodPlatform, viewPodPlatform)
    set.view(objPod, viewPod)

    start.cycling(objEgo)

    stop.cycling(objBigShuttle)
    stop.cycling(objHangerSplat1)
    stop.cycling(objHangerSplat2)
    stop.cycling(objPodPlatform)
    stop.cycling(objPod)

    draw(objEgo)
    draw(objBigShuttle)
    draw(objHangerSplat1)
    draw(objHangerSplat2)
    draw(objPodPlatform)
    draw(objPod)

    position(objBigShuttle, 90, 90)
    position(objEgo, 20, 100)
    position(objHangerSplat1, 100, 150)
    position(objHangerSplat2, 40, 90)
    position(objPodPlatform, 55, 109)
    position(objPod, 60, 100)
    set.priority(objPod, 15)
    set.priority(objEgo, 18)

    assignn(vSweeping, valTrue)
    assignn(vSplat1Cleaned, valFalse)
    assignn(vSplat2Cleaned, valFalse)
    assignn(vHasOrders, valFalse)
    assignn(vCleanedUp, valFalse)
    assignn(vTimer, 0)

}

if(!equaln(vSweeping, valTrue)){
    if(equaln(vEgoDirection, directionStopped)) {
        stop.cycling(objEgo)
    }
}

if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("stop", "sweeping")) {
    set.view(objEgo, viewRogerSuitA)
    assignn(vSweeping, valFalse)
}

if(said("start", "sweeping")) {
    set.view(objEgo, viewRogerSuitBroom)
    assignn(vSweeping, valTrue)
    start.cycling(objEgo)
}


if(said("enter", "pod")) {
    if(posn(objEgo, 53, 93, 60, 109)) {
        if(equaln(vHasOrders, valTrue)) {
            print("You climb into the pod.")
            assignn(vRoomInited, 0)
            assignn(0, 3)    
        }
        if(!equaln(vHasOrders, valTrue)) {
            print("Your orders are to clean the orbital station's shuttle bay. You better finish the job.")
        }
    }
    if(!posn(objEgo, 53, 93, 60, 109)) {
        print("You are too far away")
    }
}

if(posn(objEgo, 85, 139, 105, 151)) {
    if(!equaln(vSplat1Cleaned, valTrue)) {
        if(equaln(vSweeping, valFalse)) {
            print("You died.")
        }    
        if(equaln(vSweeping, valTrue)) {
            assignn(vSplat1Cleaned, valTrue)
            erase(objHangerSplat1)

            if(equaln(vSplat2Cleaned, valTrue)) {
                assignn(vCleanedUp, valTrue)
            }
        }    
    }
}

if(posn(objEgo, 36, 80, 48, 95)) {
    if(!equaln(vSplat2Cleaned, valTrue)) {
        if(equaln(vSweeping, valFalse)) {
            print("You died.")
        }    
        if(equaln(vSweeping, valTrue)) {
            assignn(vSplat2Cleaned, valTrue)
            erase(objHangerSplat2)

            if(equaln(vSplat1Cleaned, valTrue)) {
                assignn(vCleanedUp, valTrue)
            }
        }    
    }
}

if(equaln(vCleanedUp, valTrue)) {
    if(equaln(vTimer, 100)) {
        assignn(vRoomInited, 0)
        assignn(0, 3)
    }

    if(!equaln(vHasOrders, valTrue)) {
        addn(vTimer, 1)
    }
}

return()
#message 1 "ABC"