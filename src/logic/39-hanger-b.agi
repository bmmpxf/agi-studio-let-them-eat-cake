#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)    
    load.view(viewRogerSuitA)
    load.view(viewPodPlatform)
    load.view(viewPod)

    animate.obj(objEgo)

    set.view(objPod, viewPod)
    set.view(objPodPlatform, viewPodPlatform)

    start.cycling(objEgo)
    stop.cycling(objPodPlatform)
    stop.cycling(objPod)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            set(fWearingSuit)
        }
    }

    assignn(vEgoX, 150)
    assignn(vEgoY, 144)

    if(isset(fWearingSuit)) {
        set.view(objEgo, viewRogerSuitA)
    }
    if(!isset(fWearingSuit)) {
        set.view(objEgo, viewRoger)
    }

    draw(objEgo)
    draw(objPodPlatform)
    draw(objPod)

    set.priority(objPod, 12)
    position(objPodPlatform, 55, 109)
    position(objPod, 60, 100)
    set.horizon(73) 

    position.v(objEgo, vEgoX, vEgoY)    
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("enter", "pod")) {
    if(posn(objEgo, 49, 93, 60, 109)) {
        print("You climb into the pod.")
        addn(vCurrentScore, 1)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picInPod)    
    }
    if(!posn(objEgo, 49, 93, 60, 109)) {
        print("You are too far away")
    }
}

if(obj.in.box(objEgo, 151, 140, 155, 150)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 10)
    assignn(vEgoY, 126)
    assignn(vRoomInited, valFalse)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vCurrentRoom, picOuterBay)         
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        stop.cycling(objEgo)
        set.view(objEgo, viewRogerSuitA)
        set.priority(objEgo, 4)
        move.obj(objEgo, 100, 40, 1, fAdrift)
        // DEAD: ADRIFT 
        assignn(vDeathReason, valXsSpaceWalk)
        set(fDead)
    }
}
if(isset(fAdrift)) {
    erase(objEgo)
}

if(!isset(fWearingSuit)) {
    // DEAD: DECOMP
    reset(fWearingSuit)
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 100)
    move.obj.v(objEgo, vEgoX, vEgoY, 1, fDecompressing)
}

if(isset(fDecompressing)) {
    reset(fDecompressing)
    if(!isset(fDead)) {
        load.view(viewRogerExplode)
        set.view(objEgo, viewRogerExplode)
        set.loop(objEgo, 0)
        set.cel(objEgo, 0)
        start.cycling(objEgo)
        end.of.loop(objEgo, fDecompressed) 
    }
}

if(isset(fDecompressed)) {
    reset(fDecompressed)
    assignn(vDeathReason, valXsNoSuitExplode)    
    set(fDead)
}

return()
#message 1 "Hanger Bay 2"