#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vRoomInited, 1)

    load.view(viewRoger)
    load.view(viewRogerSwim)
    load.view(viewMist)
    load.view(viewWitchDoor)
   
    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)
    set.view(objWitchDoor, viewWitchDoor)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)
    stop.cycling(objWitchDoor)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)
    draw(objWitchDoor)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
            assignn(vEgoY, 130)
        }
    }

    reset(fDoAction)

    position(objMist, 1, 166)
    position(objMist2, 43, 166)
    position(objMist3, 85, 166)
    position(objMist4, 127, 166)
    position(objWitchDoor, 45, 129)

    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(50)
    set.priority(objWitchDoor, 10)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 155)
        assignn(vEgoDirection, directionUp)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavLakeD4)   
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 15)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWizardE5)     
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 15)
        assignn(vEgoDirection, directionRight)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("open", "door")) {
    end.of.loop(objWitchDoor, fDoAction)
}

if(isset(fDoAction)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 50)
    assignn(vEgoY, 150)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picDavWitchHouseInside)
}

return()
#message 1 "R11"
