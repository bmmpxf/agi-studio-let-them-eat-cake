#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vRoomInited, 1)

    load.view(viewRoger)
    load.view(viewRogerSplash)
    load.view(viewRogerSwim)
    load.view(viewMist)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 100)
            assignn(vEgoY, 100)
        }
    }
    
    position(objMist, 1, 166)
    position(objMist2, 43, 166)
    position(objMist3, 85, 166)
    position(objMist4, 127, 166)

    position.v(objEgo, vEgoX, vEgoY)
    assignn(vEgoEdgeTouched, edgeNone)
    set.horizon(40)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        assignn(vEgoY, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWolfE1)   
    }

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavLakeE2)     
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoY, 100)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavDaggerF2)     
    }

    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 155)
        assignn(vEgoDirection, directionUp)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}

if(isset(0)) {
    if(!isset(fSwimming)) {
        if(!isset(fDrowning)){
            set.view(objEgo, viewRogerSplash)
            set(fDrowning)
            assignn(vEgoDirection, directionStopped)
            program.control()
        }
    }    
}

if(!isset(0)) {
    if(isset(fSwimming)) {
        set.view(objEgo, viewRoger)
        reset(fSwimming)
    }
}

if(said("swim")) {
    if(isset(0)) {
        if(isset(fDrowning)) {
            set.view(objEgo, viewRogerSwim)
            reset(fDrowning)
            set(fSwimming)
            player.control()
            print("ok")
       } 
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("look", "room")) {
    print("There's a large beautiful lake that goes off to the west here and a thick fog to the south.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. To more you look into it the most lost you feel.")
}
if(said("look", "water")) {
    print("The lake is calm and beautiful.")
}

return()
#message 1 "E2 Lake"
