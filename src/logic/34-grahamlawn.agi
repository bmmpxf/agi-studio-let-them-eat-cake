#include "0-defines.aqi"

load.sound(1)

if(equaln(vRoomInited, 0)) {

    unanimate.all()
    assignn(vRoomInited, 1)
    load.view(viewCabinDoor)
    load.view(viewGrahamHatNoFeather)
    load.view(viewHawk)
    load.view(viewFeatherFall)

    animate.obj(objEgo)
    animate.obj(objHawk)
    animate.obj(objFeatherFall)
    animate.obj(objCabinDoor)

    set.view(objEgo, viewGrahamHatNoFeather)
    set.view(objHawk, viewHawk)
    set.view(objFeatherFall, viewFeatherFall)
    set.view(objCabinDoor, viewCabinDoor)

    draw(objHawk)
    draw(objFeatherFall)
    draw(objCabinDoor)
    draw(objEgo)

    stop.cycling(objCabinDoor)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 119)
            assignn(vEgoY, 126)
        }
    }

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)

    set.priority(objCabinDoor, 4)
    position(objCabinDoor, 121, 130)

    set.priority(objHawk, 10)    
    position(objHawk, 160, 70)
    move.obj(objHawk, 40, 40, 2, fDoAction)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 0)
    assignn(vEgoY, 0)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picGrahamCastle)     
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(isset(fDoAction)) {
    reset(fDoAction)
    position(objFeatherFall, 40, 40)
    move.obj(objHawk, 0, 40, 1, fUndoAction)
    move.obj(objFeatherFall, 40, 105, 1, fGotcha)
}

if(isset(fGotcha)) {
    reset(fGotcha)
    stop.cycling(objFeatherFall)
}

if(isset(fUndoAction)) {
    erase(objHawk)
}

return()
#message 1 "Grahams Lawn"