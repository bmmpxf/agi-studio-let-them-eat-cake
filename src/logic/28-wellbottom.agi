#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()

    assignn(vRoomInited, valTrue)

    load.view(viewRogerBucket)
    load.view(viewRogerSplash)
    load.view(viewRogerSwim)
    load.view(viewRogerClimbRope)
    load.view(viewBucket)
    load.view(viewRope)
    

    animate.obj(objEgo)
    animate.obj(objRope)

    set.view(objRope, viewRope)   
    set.view(objRope2, viewRope)  

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            reset(fUndoAction)
            reset(fBucketDown)

            set.view(objEgo, viewRogerBucket)

            draw(objEgo)
            draw(objRope)
            draw(objRope2)

            position(objEgo, 76, 0)
            position(objRope, 81, 0)
            move.obj(objRope, 81, 120, 3, fUndoAction)
            move.obj(objEgo, 76, 147, 3, fDoAction)
        
            stop.cycling(objEgo)        
        }
    }
    if(!equaln(vEgoY, 0)) {
        assignn(vEgoEdgeTouched, edgeNone)

        set.view(objEgo, viewRogerSwim)
        set.view(objBucket, viewBucket)

        draw(objEgo)
        draw(objRope)
        draw(objRope2)
        draw(objBucket)

        stop.cycling(objBucket)        

        position(objRope, 81, 120)
        position(objRope2, 81, 180)
        position(objBucket, 76, 152)
        set.cel(objBucket, 0)        

        position(objEgo, 81, 167)
    }

    set.horizon(0)   
}

if(isset(fUndoAction)) {
    reset(fUndoAction)
    position(objRope2, 81, 152)
    end.of.loop(objEgo)
    normal.motion(objEgo)
}

if(isset(0)) {
    // on water
    if(!isset(fSwimming)) {
        set(fDrowning)
    }
}

if(said("swim")) {
    if(isset(fDrowning)) {
        set.view(objEgo, viewRogerSwim)
        reset(fDrowning)
        set(fSwimming)
        addn(vCurPoints, 1)
        player.control()
        print("ok")
    } 
}

if(said("dive")) {
    addn(vCurPoints, 1)
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoY, 50)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vRoomInited, valFalse)
    assignn(vCurrentRoom, picDavWellDive)   
}

if(said("look")) {
    print("You are at the bottom of the ancient well.")
}
if(said("look", "room")) {
    print("You are at the bottom of the ancient well.")
}

if(said("exit", "bucket")) {
    set.view(objBucket, viewBucket)

    draw(objBucket)

    stop.cycling(objBucket)
    
    position(objBucket, 76, 152)
    set.cel(objBucket, 0)
    
    position(objRope2, 81, 180)
    position(objEgo, 81, 167)

    set(fDrowning)
    set.view(objEgo, viewRogerSplash)
    start.cycling(objEgo)
    addn(vCurPoints, 1)
}

if(said("enter", "bucket")) {
    erase(objBucket)
    set.view(objEgo, viewRogerBucket)

    position(objRope2, 81, 180)
    position(objEgo, 76, 181)

    stop.cycling(objEgo)        
}

if(said("climb", "rope")) {
    set.view(objBucket, viewBucket)

    draw(objBucket)

    stop.cycling(objBucket)
    
    position(objBucket, 76, 152)
    set.cel(objBucket, 0)
    
    position(objRope2, 81, 180)
    position(objEgo, 80, 123)

    set.view(objEgo, viewRogerClimbRope)
    addn(vCurPoints, 1)

}

if(said("take", "breath")) {
    print("You take a deep breath.")
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 42)
        assignn(vEgoY, 109)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWellTopD1)
        print("You climb out of the bucket and well. In a uncharacteristic moment of thoughtfulness and remembering to 'be kind and rewind' you laboriously return the bucket to it's original location.")     
    }
}

if(said("look", "water")) {
    print("It's water. Wet. Hydrated.")
}
if(said("look", "rope")) {
    print("The rope reaches up to the top of the well.")
}
if(said("look", "bucket")) {
    print("You gotta admit that it is a nice bucket.")
}

return()

#message 1 "Well Bottom"