#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {
    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewRogerSplash)
    load.view(viewRogerSwim)
    load.view(viewCastleFlags)
    load.view(viewCastleCrocs)
    load.view(viewMist)

    animate.obj(objEgo)
    animate.obj(objCastleFlags)
    animate.obj(objCastleCrocs)

    set.view(objEgo, viewRoger)
    set.view(objCastleFlags, viewCastleFlags)
    set.view(objCastleCrocs, viewCastleCrocs)
    set.view(objMist, viewMist)
    stop.cycling(objMist)

    //object.on.water(objCastleCrocs)

    draw(objEgo)
    draw(objCastleFlags)
    draw(objCastleCrocs)
    draw(objMist)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
            assignn(vEgoY, 130)
        }
    }

    stop.cycling(objEgo)

    assignn(vEgoEdgeTouched, edgeNone)

    position(objMist, 80, 40)
    position(objCastleFlags, 5, 20)
    position(objCastleCrocs, 50, 165)

    position.v(objEgo, vEgoX, vEgoY)
    set.horizon(40)

}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeTop)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 45)
        assignn(vEgoDirection, directionDown)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavCastleLeftA2)     
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoDirection, directionLeft)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}

if(isset(0)) {
    if(!isset(fSwimming)) {
        if(!isset(fDrowning)){
            set.view(objEgo, viewRogerSplash)
            set(fDrowning)
            assignn(vEgoDirection, directionStopped)
            program.control()
        }
    }    
}

if(!isset(0)) {
    if(isset(fSwimming)) {
        set.view(objEgo, viewRoger)
        reset(fSwimming)
    }
}

if(said("swim")) {
    if(isset(0)) {
        if(isset(fDrowning)) {
            set.view(objEgo, viewRogerSwim)
            reset(fDrowning)
            set(fSwimming)
            player.control()
            print("ok")
       } 
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    if(!isset(fDrowning)) {
        if(!isset(fSwimming)){
            stop.cycling(objEgo)
        }
    }
}

if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("look", "room")) {
    print("You are at the east end of a castle. There is a thick fog to the north and east.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. To more you look into it the most lost you feel.")
}


return()
#message 1 "Castle Right"