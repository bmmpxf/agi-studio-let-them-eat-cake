#include "0-defines.aqi"


if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vRoomInited, 1)

    load.view(viewRoger)
    load.view(viewRogerSplash)
    load.view(viewRogerSwim)    
    load.view(viewMist)
    load.view(viewFGM)
    
    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)
    set.view(objFGM, viewFGM)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)
    draw(objFGM)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
            assignn(vEgoY, 70)
        }
    }

    position(objMist, 1, 43)
    position(objMist2, 43, 43)
    position(objMist3, 85, 43)
    position(objMist4, 127, 43)

    if(has(invInvitation)) {
        if(!isset(fEyesOpen)) {
            if(!isset(fFlashed)) {
                position(objFGM, 141, 154)
                follow.ego(objFGM, 1, fGotcha)    
            }
        }
    }

    reset(fSwimming)
    reset(fDrowning)
    reset(fGotcha)

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)
    set.horizon(50)
}

if(isset(fGotcha)) {
    reset(fGotcha)
    erase(objFGM)
    set(fEyesOpen)
    print("A strange female alien dressed in beautiful shimmering robes catches up to you. In a voice that can only be described as song she says 'Traveler though you are asleep by may spell your eyes are opened.' A wave of energy errupts from a gadget she is carrying and blasts right into you. With a 'poof' she disappears. Based on your past experience with sudden and unexpected encounters of the 'energy burst' kind you assume firstly that you have been hit with a laser and secondly that your body is now as chard as the surface of Ortega. Contrary to your expectations of cause and effect you find you have no injuries at all. What in Fester Blatz World o Wonders was that all about?!")
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWitchHouseD5)             
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoDirection, directionDown)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavRockE4)     
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 15)
        assignn(vEgoDirection, directionRight)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }
}

if(isset(fSysEgoOnWater)) {
    if(!isset(fSwimming)) {
        if(!isset(fDrowning)){
            set.view(objEgo, viewRogerSplash)
            set(fDrowning)
        }
    }    
}

if(!isset(fSysEgoOnWater)) {
    if(isset(fSwimming)) {
        set.view(objEgo, viewRoger)
        reset(fSwimming)
    }
    if(isset(fDrowning)) {
        set.view(objEgo, viewRoger)
        reset(fDrowning)    
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    if(!isset(fSysEgoOnWater)) {
        stop.cycling(objEgo)
    }
}

if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("swim")) {
    if(isset(fSysEgoOnWater)) {
        if(isset(fDrowning)) {
            set.view(objEgo, viewRogerSwim)
            reset(fDrowning)
            set(fSwimming)
            player.control()
            print("ok")
       } 
    }
}

if(said("look")) {
    print("There's a beautiful lake here and a thick fog to the north and west.")
}
if(said("look", "room")) {
    print("There's a beautiful lake here and a thick fog to the north and west.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. The more you look into it the more lost you feel.")
}
if(said("look", "water")) {
    print("The lake is calm and beautiful.")
}

return()
#message 1 "D4 Lake"
