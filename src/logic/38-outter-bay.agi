#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewRoger)
    load.view(viewTram)
    load.view(viewRogerSuitA)
    load.view(viewLockedDoor)
    load.view(viewPlate)
 
    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objTram, viewTram)
    set.view(objHangingSuit1, viewRogerSuitA)
    set.view(objHangerDoorLeft, viewLockedDoor)
    set.view(objPlate, viewPlate)

    draw(objTram)
    draw(objHangingSuit1)
    draw(objHangerDoorLeft)
    draw(objPlate)

    stop.cycling(objTram)
    stop.cycling(objHangingSuit1)
    stop.cycling(objHangerDoorLeft)
    stop.cycling(objPlate)

    if(equaln(vEgoX, 10)) {
        if(equaln(vEgoY, 126)) {
            // returned or debug teleport
            set.view(objEgo, viewRogerSuitA)
            draw(objEgo)
            position(objTram, 55, 47)
            set.priority(objTram, 4)
            position.v(objEgo, vEgoX, vEgoY)
        }
    }
    if(!equaln(vEgoY, 126)) {
        // sent into the room via tram 
        set.cel(objTram, 4)
        set.loop(objHangingSuit1, 2)
        position(objHangingSuit1, 35, 95)

        set(fTramRunning)
        set.priority(objTram, 4)
        position(objTram, 5, 47)
        move.obj(objTram, 55, 47, 2, fTramInboundDone)        
    }

    position(objHangerDoorLeft, 5, 129)
    set.priority(objPlate, 15)
    position(objPlate, 85, 118)
    assignn(vTimer, 0)
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}

if(said("exit", "tram")) {  
    set.cel(objTram, 0) 
    position(objEgo, 55, 50)
    draw(objEgo)
}

if(said("enter", "tram")) {
    print("You pod is fueled and waiting... Better get out to the hanger.")
}

if(isset(fTramInboundDone)) {
    reset(fTramInboundDone)
    set.cel(objTram, 1)
}

if(said("wear", "suit")) {
    set.view(objEgo, viewRogerSuitA)
    erase(objHangingSuit1)
    set(fWearingSuit)
    addn(vCurrentScore, 1)
    print("Ok")
}

if(posn(objEgo, 8, 123, 20, 143)) {
    end.of.loop(objHangerDoorLeft, fDoAction)
}
if(obj.in.box(objEgo, 21, 123, 25, 128)) {
    set.cel(objHangerDoorLeft, 0)
    //end.of.loop(objHangerDoorLeft, 99)
}

if(obj.in.box(objEgo, 2, 123, 5, 128)) {
    get.posn(objEgo, vEgoX, vEgoY)
    assignn(vEgoX, 146)
    assignn(vEgoY, 141)
    assignn(vRoomInited, valFalse)
    assignn(vEgoEdgeTouched, edgeNone)
    assignn(vCurrentRoom, picHangerB)     
}



return()
#message 1 "OutterBay"
