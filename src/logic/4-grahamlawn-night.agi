#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewGrahamNoHat)
    load.view(viewGrahamHatNoFeather)
    load.view(viewWolf)
    load.view(viewCabinDoor)

    animate.obj(objEgo)
    animate.obj(objWolf1)
    animate.obj(objWolf2)
    animate.obj(objCabinDoor)

    set.view(objWolf1, viewWolf)
    set.view(objWolf2, viewWolf)
    set.view(objCabinDoor, viewCabinDoor)

    draw(objWolf1)
    draw(objWolf2)
    draw(objCabinDoor)
    draw(objEgo)

    stop.cycling(objWolf1)
    stop.cycling(objWolf2)
    stop.cycling(objCabinDoor)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 119)
            assignn(vEgoY, 126)
        }
    }

    reset(fSoundDone)
    reset(fDoAction)
    reset(fGotcha)

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)

    position(objWolf1, 15, 40)
    position(objWolf2, 170, 160)
    position(objCabinDoor, 121, 130)
    set.priority(objCabinDoor, 4)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeRight)) {
    }    
}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}


if(!isset(fDead)) { 
    if(!obj.in.box(objEgo, 100, 116, 158, 130)) {
        if(!isset(fRunForIt)) {
            set(fRunForIt)
            load.sound(sndDevMonster)
            sound(sndDevMonster, fSoundDone)    
        }
    }

    if(isset(fSoundDone)) {
        if(!isset(fDoAction)) {
            set(fDoAction)
        }
    }

    if(isset(fDoAction)) {
        reset(fSoundDone)
        start.cycling(objWolf1)
        follow.ego(objWolf1, 3, fGotcha)

        start.cycling(objWolf2)
        follow.ego(objWolf2, 3, fGotcha)
    }

    if(isset(fGotcha)) {
        erase(objEgo)

        set(fDead) 
        assignn(vDeathReason, valXsWolfMunchGraham)
        load.view(viewFightCloud)
        

        //set.view(objWolf1, viewFightCloud)
        set.view(objWolf2, viewFightCloud)
        
        start.cycling(objWolf1)
        start.cycling(objWolf2)
        wander(objWolf1)         
        wander(objWolf2)         
    }
}

return()

#message 1 "Grahams Lawn Night"