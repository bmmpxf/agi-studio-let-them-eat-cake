#include "0-defines.aqi"

if(equaln(vRoomInited, valFalse)) {

    unanimate.all()
    assignn(vRoomInited, valTrue)

    load.view(viewGrahamNoHat)
    load.view(viewGrahamHatNoFeather)
    load.view(viewWolf)
    load.view(viewCabinDoor)

    animate.obj(objEgo)
    animate.obj(objWolf1)
    animate.obj(objWolf2)
    animate.obj(objCabinDoor)

    set.view(objWolf1, viewWolf)
    set.view(objWolf2, viewWolf)
    set.view(objCabinDoor, viewCabinDoor)

    draw(objWolf1)
    draw(objWolf2)
    draw(objCabinDoor)
    draw(objEgo)

    stop.cycling(objWolf1)
    stop.cycling(objWolf2)
    stop.cycling(objCabinDoor)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 119)
            assignn(vEgoY, 126)
        }
    }

    if(isset(fWearingHat)) {
        set.view(objEgo, viewGrahamHatNoFeather)
    }
    if(!isset(fWearingHat)) {
        set.view(objEgo, viewGrahamNoHat)
    }
    set.view(objWolf1, viewWolf)

    reset(fSoundDone)
    reset(fDoAction)
    reset(fGotcha)
    reset(fGotcha2)

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)

    position(objWolf1, 15, 40)
    position(objWolf2, 160, 180)
    position(objCabinDoor, 121, 130)
    set.priority(objCabinDoor, 4)
    set.priority(objEgo, 11)

}

if(equaln(vEgoDirection, directionStopped)) {
    stop.cycling(objEgo)
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}


if(!isset(fDead)) { 
    if(!obj.in.box(objEgo, 100, 116, 158, 130)) {
        if(!isset(fRunForIt)) {
            set(fRunForIt)
            load.sound(sndDevMonster)
            sound(sndDevMonster, fSoundDone)    
        }
    }

    if(isset(fSoundDone)) {
        if(!isset(fDoAction)) {
            set(fDoAction)
        }
    }

    if(isset(fDoAction)) {
        reset(fSoundDone)
        start.cycling(objWolf1)
        follow.ego(objWolf1, 3, fGotcha)

        start.cycling(objWolf2)
        follow.ego(objWolf2, 3, fGotcha2)
    }

    if(isset(fGotcha)) {
        erase(objEgo)

        set(fDead) 
        assignn(vDeathReason, valXsWolfMunchGraham)
        load.view(viewGrahamFightCloud)

        set.view(objWolf1, viewGrahamFightCloud)
        
        start.cycling(objWolf1)
        start.cycling(objWolf2)
        wander(objWolf1)         
        wander(objWolf2)         
    }
    if(isset(fGotcha2)) {
        erase(objEgo)

        set(fDead) 
        assignn(vDeathReason, valXsWolfMunchGraham)
        load.view(viewGrahamFightCloud)

        set.view(objWolf2, viewGrahamFightCloud)
        
        start.cycling(objWolf1)
        start.cycling(objWolf2)
        wander(objWolf1)         
        wander(objWolf2)         
    }

}

if(said("look")) {
    print("You are standing outside your house. It is dark out here. The landscape is cast in pale moonlight and shadow.")
}
if(said("look", "room")) {
    print("You are standing outside your house. It is dark out here. The landscape is cast in pale moonlight and shadow.")
}

if(said("wear", "hat")) {
    if(has(invHat)) {
        if(isset(fWearingHat)) {
            print("You are already wearing it.")
        }
        if(!isset(fWearingHat)) {
            set(fWearingHat)
            set.view(objEgo, viewGrahamHatNoFeather)
            print("You put on your trusty hat!")

            if(!isset(fPointsForHatWear)) {
                set(fPointsForHatWear)
                addn(vCurrentScore, 1)    
            }
        }
    }
    if(!has(invHat)) {
        print("You don't have it.")
    }
}   


if(said("remove", "hat")) {
    if(has(invHat)) {
        if(!isset(fWearingHat)) {
            print("You are not wearing your hat.")
        }
        if(isset(fWearingHat)) {
            reset(fWearingHat)
            set.view(objEgo, viewGrahamNoHat)
        }
    }
    if(!has(invHat)) {
        print("You don't have it.")
    }
}  

if(said("open", "door")) {
    if(obj.in.box(objEgo, 100, 116, 158, 130)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 35)
        assignn(vEgoY, 110)
        reset(fWearingHat)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picGrahamsHouseInside)
    }
    if(!obj.in.box(objEgo, 100, 116, 158, 130)) {
        print("You are not close enough.")
    }
}


return()

#message 1 "Grahams Lawn Night"