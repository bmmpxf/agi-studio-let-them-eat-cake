#include "0-defines.aqi"

load.sound(1)

if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vGameCycleDelay, 2)
    assignn(vRoomInited, 1)

    load.view(viewCrashingPod)
    load.view(viewRoger)
    load.view(viewRogerSplash)
    load.view(viewRogerSwim)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objCrashingPod, viewCrashingPod)

    draw(objEgo)
    draw(objCrashingPod)

    stop.cycling(objEgo)
    stop.cycling(objCrashingPod)

    set.cel(objCrashingPod, 7)

    position(objCrashingPod, 20, 140)
    position(objEgo, 15, 140)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 43)

        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavRiverBendA4)     
    }

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)

        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavOgreF3)     
    }
}

if(isset(0)) {
    if(!isset(fSwimming)) {
        if(!isset(fDrowning)){
            set.view(objEgo, viewRogerSplash)
            set(fDrowning)
            assignn(vEgoDirection, directionStopped)
            program.control()
        }
    }    
}

if(!isset(0)) {
    if(isset(fSwimming)) {
        set.view(objEgo, viewRoger)
        reset(fSwimming)
    }
}

if(said("swim")) {
    if(isset(0)) {
        if(isset(fDrowning)) {
            set.view(objEgo, viewRogerSwim)
            reset(fDrowning)
            set(fSwimming)
            player.control()
            print("ok")
       } 
    }
}

if(equaln(vEgoDirection, directionStopped)) {
    if(!isset(fDrowning)) {
        if(!isset(fSwimming)){
            stop.cycling(objEgo)
        }
    }
}
if(!equaln(vEgoDirection, directionStopped)) {
    start.cycling(objEgo)
}
    
return()
#message 1 "ABC"