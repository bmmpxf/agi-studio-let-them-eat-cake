#include "0-defines.aqi"

if(equaln(vRoomInited, 0)) {
    unanimate.all()
    assignn(vRoomInited, 1)
    assignn(vActionCount, 0)
    reset(fProgramControl)

    load.view(viewRoger)
    load.view(viewMist)

    animate.obj(objEgo)

    set.view(objEgo, viewRoger)
    set.view(objMist, viewMist)
    set.view(objMist2, viewMist)
    set.view(objMist3, viewMist)
    set.view(objMist4, viewMist)

    stop.cycling(objMist)
    stop.cycling(objMist2)
    stop.cycling(objMist3)
    stop.cycling(objMist4)

    draw(objEgo)
    draw(objMist)
    draw(objMist2)
    draw(objMist3)
    draw(objMist4)

    if(equaln(vEgoX, 0)) {
        if(equaln(vEgoY, 0)){
            assignn(vEgoX, 140)
            assignn(vEgoY, 70)
        }
    }

    position(objMist, 1, 43)
    position(objMist2, 43, 43)
    position(objMist3, 85, 43)
    position(objMist4, 127, 43)

    assignn(vEgoEdgeTouched, edgeNone)
    position.v(objEgo, vEgoX, vEgoY)
    set.horizon(50)
}

if(!equaln(vEgoEdgeTouched, edgeNone)) {
    if(equaln(vEgoEdgeTouched, edgeBottom)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavWizardE5)             
    }   

    if(equaln(vEgoEdgeTouched, edgeTop)) {
        print("You're a bit foggy here. You decide to turn back.")
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoY, 55)
        assignn(vEgoDirection, directionDown)
        set.dir(objEgo, vEgoDirection)
        position.v(objEgo, vEgoX, vEgoY)
        assignn(vEgoEdgeTouched, edgeNone)
    }

    if(equaln(vEgoEdgeTouched, edgeRight)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 5)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavDwarfF4)     
    }    

    if(equaln(vEgoEdgeTouched, edgeLeft)) {
        get.posn(objEgo, vEgoX, vEgoY)
        assignn(vEgoX, 150)
        assignn(vEgoEdgeTouched, edgeNone)
        assignn(vRoomInited, valFalse)
        assignn(vCurrentRoom, picDavLakeD4)   
    }
}

if(said("look", "hole")) {
    set(fProgramControl)
    load.view(viewRogerEaten)
    set.view(objEgo, viewRogerEaten)

    position(objEgo, 97, 138)
    assignn(vGameCycleDelay, 3)
    end.of.loop(objEgo, fDoAction)
}

if(isset(fDoAction)) {
    reset(fDoAction)
    addn(vActionCount, 1)

    if(equaln(vActionCount, 1)) {
        set.loop(objEgo, 1)
        set.cel(objEgo, 0)
        assignn(vGameCycleDelay, 3)
        end.of.loop(objEgo, fDoAction)
    }

    // Maybe we wont add the burp becuase
    // it implies who/what killed roger
    // and this hole already has a storyline
    // if(equaln(vActionCount, 2)) {
    //     load.view(viewBurp)
    //     set.view(objBurp, viewBurp)
    //     draw(objBurp)
    //     position(objBurp, 77, 128)
    // }
}

if(!isset(fProgramControl)) {
    if(equaln(vEgoDirection, directionStopped)) {
        stop.cycling(objEgo)
    }
    if(!equaln(vEgoDirection, directionStopped)) {
        start.cycling(objEgo)
    }    
}

if(said("look", "room")) {
    print("The area is heavily wooded. A large rock dominates the area.")
}
if(said("look", "fog")) {
    print("There's a thick fog here. It almost feels as much mental as it is physical. To more you look into it the most lost you feel.")
}
if(said("look", "rock")) {
    print("There's a large rock here. A large hole can be seen at the base of the rock.")
}


return()
#message 1 "Rock"
